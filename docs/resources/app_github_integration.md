---
layout: "herokux"
page_title: "HerokuX: herokux_app_github_integration"
sidebar_current: "docs-herokux-resource-app-github-integration"
description: |-
Provides a resource to manage the integration between a Heroku app and GitHub repository.
---

# herokux_app_github_integration

This resource manages the integration between a Heroku app and GitHub repository.

Currently, this resource is only able to set up [automatic deploys](https://devcenter.heroku.com/articles/github-integration#automatic-deploys)
for a GitHub branch on a Heroku app.

-> **IMPORTANT!**
In order to use this resource, the user authenticated to the provider must have had already [authorized GitHub](https://devcenter.heroku.com/articles/github-integration#enabling-github-integration)
with their Heroku account. This authorization can be done via the 'Deploy' tab for a Heroku app.
Please note that revoking this authorization after you have used this resource may result in a broken Terraform configuration.

## Example Usage

```hcl-terraform
// Create an app.
resource "heroku_app" "staging" {
  name   = "my-cool-app-stg"
  region = "us"

  organization {
    name = "my-cool-team"
  }
}

// Create a pipeline.
resource "heroku_pipeline" "foobar" {
  name = "foobar-pipeline"
}

# Couple app to pipeline.
resource "heroku_pipeline_coupling" "staging" {
  app      = heroku_app.staging.id
  pipeline = heroku_pipeline.foobar.id
  stage    = "staging"
}

// Add the GitHub repository integration with the pipeline.
resource "herokux_pipeline_github_integration" "foobar" {
  pipeline_id = heroku_pipeline.foobar.id
  org_repo = "myorg/myrepo"
}

// Add Heroku app GitHub integration.
resource "herokux_app_github_integration" "foobar" {
  app_id = heroku_app.staging.uuid
  branch = "main"
  auto_deploy = true
  wait_for_ci = true

  # Tells Terraform that this resource must be created/updated
  # only after the `herokux_pipeline_github_integration` has been successfully applied.
  depends_on = [herokux_pipeline_github_integration.foobar]
}
```

## Argument Reference

The following arguments are supported:

* `app_id` - (Required) `<string>` The UUID for a Heroku app.
* `branch` - (Required) `<string>` The name of the GitHub repository branch.
* `auto_deploy` - (Required) `<boolean>` Enable/disable automatic deploys from GitHub.
* `wait_for_ci` - (Optional) `<boolean>` Wait for CI to pass before deploy. Only enable this option if you have a
  Continuous Integration service configured on your repo.

## Attributes Reference

The following attributes are exported:

* `id` - The app ID.
* `repository` - The full name of the repository. For example, `myorg/myrepo`.
* `repository_id` - The ID of the GitHub repository. This ID comes from GitHub and is not generated by Heroku.
* `integration_id` - The integration ID.

## Import

An existing Heroku app GitHub integration can be imported using the app UUID.

For example:

```shell script
$ terraform import herokux_app_github_integration.foobar "2508ebbd-74bb-4e81-a63c-d193d2bd5716"
```
